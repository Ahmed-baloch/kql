SecurityAlert
| where AlertName has "Unusual number of failed sign-in attempts" and ProviderName == "Azure Security Center"
| summarize
    TimeGenerated = min(TimeGenerated),
    minTimeGenerated = min(TimeGenerated),
    maxTimeGenerated = max(TimeGenerated),
    take_any(RemediationSteps, ExtendedLinks, AlertLink, CompromisedEntity, Tactics),
    Entities = make_set(todynamic(Entities))
    by AlertName, AlertSeverity, AlertType, Description, ProviderName, ResourceId
| mv-apply Entities on (
    extend
        EntityValue = tostring(Entities[tostring(bag_keys(Entities)[1])]),
        EntityType = tostring(Entities["Type"])
    | summarize take_any(Entities) by EntityValue, EntityType
    | sort by EntityType desc
    | summarize Entities = make_list(Entities)
    )
| mv-apply with_itemindex = Index_aux Entities on (
    extend Entities = bag_merge(bag_pack(@"$id", Index_aux + 2), bag_remove_keys(Entities, dynamic(["$id"])))
    | summarize Entities = tostring(array_sort_asc(make_list(Entities)))
    )
| project
    TimeGenerated,
    AlertName, 
    AlertSeverity,
    Description,
    CompromisedEntity,
    minTimeGenerated,
    maxTimeGenerated,
    ResourceId,
    AlertLink,
    RemediationSteps,
    ExtendedLinks,
    ProviderName,
    AlertType,
    Tactics,
    Entities
