SecurityAlert
| where AlertName has "Suspicious request to Kubernetes API" and ProviderName != "ASI Scheduled Alerts"
| extend ExtendedProperties = todynamic(ExtendedProperties)
| extend
    APIRequest = tostring(ExtendedProperties["API Request"]),
    ContainerID = tostring(ExtendedProperties["Container ID"]),
    ImageName = tostring(ExtendedProperties["Image Name"]),
    UserName = tostring(ExtendedProperties["User Name"]),
    ResourceType = tostring(ExtendedProperties["resourceType"])
| summarize
    TimeGenerated = min(TimeGenerated),
    StartTime = min(StartTime),
    EndTime = max(EndTime),
    ContainerIDs = make_set(ContainerID, 250),
    AlertLinks = make_list(AlertLink, 250),
    Entities = make_set(todynamic(Entities)),
    take_any(RemediationSteps)
    by AlertName, AlertSeverity, Description, ResourceId, APIRequest, ImageName, UserName, ResourceType, Tactics
| mv-apply Entities on (
    extend
        EntityValue = tostring(Entities[tostring(bag_keys(Entities)[1])]),
        EntityType = tostring(Entities["Type"])
    | where isnotempty(EntityType)
    | summarize take_any(Entities) by EntityValue, EntityType
    | sort by EntityType desc
    | summarize Entities = make_list(Entities)
    )
| mv-apply with_itemindex = Index_aux Entities on (
    extend Entities = bag_merge(bag_pack(@"$id", tostring(Index_aux + 2)), bag_remove_keys(Entities, dynamic(["$id"])))
    | summarize Entities = tostring(array_sort_asc(make_list(Entities)))
    )
| project
    TimeGenerated,
    AlertName,
    AlertSeverity,
    Description,
    RemediationSteps,
    ResourceId,
    ResourceType,
    StartTime,
    EndTime,
    ImageName,
    UserName,
    APIRequest,
    ContainerIDs,
    AlertLinks,
    Tactics,
    Entities
