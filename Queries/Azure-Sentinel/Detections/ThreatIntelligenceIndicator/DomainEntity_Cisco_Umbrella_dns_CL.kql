// This query assumes a feed of threat indicators is ingested/synchronized periodically, and each synchronization ingests all the indicators that are to be monitored.
// Additionally, threat indicators that were added manually in Sentinel are renovated as ThreatIntelligenceIndicators events every ~12 days.
// Matching indicators with table events will require taking into account these different processes.
// The function defined below has some datetime parameters, these will affect only the feed threat indicators, not the manually added indicators.
let query_frequency = 1h;
let query_period = 14d;
let ti_feed_sync_frequency = 1d;
let ti_feed_sync_duration = 6h;
let table_query_lookback = 12h;
let _TIBenignProperty =
    _GetWatchlist('ID-TIBenignProperty')
    | where Notes has_any ("[SourceDomain]", "[DestinationDomain]")
    | project IndicatorId, BenignProperty
;
let _TIExcludedSources = toscalar(
    _GetWatchlist('Activity-ExpectedSignificantActivity')
    | where Activity == "ThreatIndicatorSource"
    | summarize make_list(Auxiliar)
    );
let _TITableMatch = (ti_start: datetime, only_new_ti: boolean, table_start: datetime, table_end: datetime) {
    // Scheduled Analytics rules have a query period limit of 14d
    let ti_feed_start = max_of(ago(query_period), ti_start - iff(only_new_ti, ti_feed_sync_frequency + ti_feed_sync_duration, 0s));
    let ti_feed_end = now();
    let _Indicators =// materialize(
        union
            // Manually added indicators
            (ThreatIntelligenceIndicator
            | where TimeGenerated > ago(query_period) and ExternalIndicatorId has "indicator--"
            ),
            // Feed ingested indicators
            (ThreatIntelligenceIndicator
            | where TimeGenerated between (ti_feed_start .. ti_feed_end) and not(ExternalIndicatorId has "indicator--")
            )
        // Take the earliest TimeGenerated and the latest column info
        | summarize hint.strategy=shuffle
            minTimeGenerated = min(TimeGenerated),
            arg_max(TimeGenerated, Active, Description, ActivityGroupNames, IndicatorId, ThreatType, DomainName, Url, ExpirationDateTime, ConfidenceScore, AdditionalInformation, ExternalIndicatorId)
            by IndicatorId
        // Remove inactive or expired indicators
        | where not(not(Active) or ExpirationDateTime < ti_feed_end)
        // Remove feed indicators not received in the specified period
        | where not(TimeGenerated < ti_start and not(ExternalIndicatorId has "indicator--"))
        // Pick indicators that contain the desired entity type
        | where isnotempty(DomainName)
        | extend Domain = tolower(DomainName)
        // Remove indicators from specific sources
        | where not(AdditionalInformation has_any (_TIExcludedSources))
        // Remove excluded indicators with benign properties
        | join kind=leftanti _TIBenignProperty on IndicatorId, $left.Domain == $right.BenignProperty
        // Deduplicate indicators by Domain column, equivalent to using join kind=innerunique afterwards
        | summarize hint.strategy=shuffle
            minTimeGenerated = min(minTimeGenerated),
            take_any(*)
            by Domain
        // If we want only new indicators, remove indicators received previously
        | where not(only_new_ti and minTimeGenerated < ti_start)
    //)
    ;
    //let _IndicatorsLength = toscalar(_Indicators | summarize count());
    //let _IndicatorsPrefilter = toscalar(
    //    _Indicators
    //    | extend AuxiliarField = tostring(split(Domain, ".")[-1])
    //    | summarize make_set_if(AuxiliarField, isnotempty(AuxiliarField))
    //);
    //let _IndicatorsPrefilterLength = array_length(_IndicatorsPrefilter);
    let _TableEvents =
        Cisco_Umbrella_dns_CL
        | where TimeGenerated between (table_start .. table_end)
        // Filter events that may contain indicators
        | where isnotempty(Domain_s)
        //| where not(_IndicatorsPrefilterLength < 10000 and not(Domain_s has_any (_IndicatorsPrefilter))) // valid TLD ~1500 , "has_any" limit 10000
        | summarize hint.strategy=shuffle take_any(*) by OriginalDomain = trim_end(@"\.", tolower(Domain_s))
        //| where not(_IndicatorsPrefilterLength < 10000 and not(tostring(split(OriginalDomain, ".")[-1]) in (_IndicatorsPrefilter)))
        | extend SplitLevelDomains = split(OriginalDomain, ".")
        | mv-expand Level = range(0, array_length(SplitLevelDomains) - 2) to typeof(int)
        | extend Domain = strcat_array(array_slice(SplitLevelDomains, Level, -1), ".")
        //| where not(_IndicatorsLength < 1000000 and not(Domain in (toscalar(_Indicators | summarize make_list(Domain))))) // "in" limit 1.000.000
        | project-rename Cisco_Umbrella_dns_CL_TimeGenerated = TimeGenerated
    ;
    _Indicators
    | join kind=inner hint.strategy=shuffle _TableEvents on Domain
    // Take only a single event by key columns
    //| summarize hint.strategy=shuffle take_any(*) by Domain, Identities_s
    | project
        Cisco_Umbrella_dns_CL_TimeGenerated,
        Description, ActivityGroupNames, IndicatorId, ThreatType, DomainName, Url, ExpirationDateTime, ConfidenceScore, AdditionalInformation,
        EventStartTime = todatetime(column_ifexists('Timestamp_t', column_ifexists('Timestamp_s', ''))),
        Dvc = "CiscoUmbrella",
        DvcAction = column_ifexists('Action_s', ''),
        Identities_s,
        Identity_Types_s,
        SrcIpAddr = column_ifexists('InternalIp_s', ''),
        SrcNatIpAddr = column_ifexists('ExternalIp_s', ''),
        DnsQuery = trim_end(@'\.', column_ifexists('Domain_s', '')),
        EventResult = iff(ResponseCode_s =~ 'NOERROR', 'Success', 'Failure'),
        EventResultDetails = ResponseCode_s,  // => ResponseCodeNames
        UrlCategory = column_ifexists('Categories_s', ''),
        ThreatCategory = column_ifexists('Blocked_Categories_s', ''),
        IdentityTypes = column_ifexists('Identity_Types_s', ''),
        Identities = column_ifexists('Identities_s', ''),
        PolicyIdentityType = column_ifexists('Policy_Identity_Type_s', ''),
        PolicyIdentity = column_ifexists('Policy_Identity_s', '')                
    | mv-apply
        Identities_s_aux = todynamic(Identities_s) to typeof(string),
        Identity_Types_s_aux = todynamic(Identity_Types_s) to typeof(string) on ( 
        summarize IdentitiesDict = make_bag(pack(Identity_Types_s_aux, Identities_s_aux))
        )
    | extend
        HostName = tostring(IdentitiesDict["AD Computers"]),
        UserPrincipalName = tostring(IdentitiesDict["AD Users"])
};
union// isfuzzy=true
    // Match      all indicators available                                    with current table events
    _TITableMatch(ago(ti_feed_sync_frequency + ti_feed_sync_duration), false, ago(query_frequency), now()),
    // Match      new indicators since last query execution                   with past table events
    _TITableMatch(ago(query_frequency), true, ago(table_query_lookback), ago(query_frequency))
| summarize arg_max(Cisco_Umbrella_dns_CL_TimeGenerated, *) by IndicatorId, Identities_s
| extend
    timestamp = Cisco_Umbrella_dns_CL_TimeGenerated,
    HostCustomEntity = HostName,
    AccountCustomEntity = UserPrincipalName,
    URLCustomEntity = Url
