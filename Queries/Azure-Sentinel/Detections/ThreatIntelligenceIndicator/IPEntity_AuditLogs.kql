// This query assumes a feed of threat indicators is ingested/synchronized periodically, and each synchronization ingests all the indicators that are to be monitored.
// Additionally, threat indicators that were added manually in Sentinel are renovated as ThreatIntelligenceIndicator events every ~12 days.
// Matching indicators with table events will require taking into account these different processes.
// The function defined below has some datetime parameters, these will affect only the feed threat indicators, not the manually added indicators.
let query_frequency = 1h;
let query_period = 14d;
let ti_feed_sync_frequency = 1d;
let ti_feed_sync_duration = 6h;
let table_query_lookback = 14d;
let _TIBenignProperty =
    _GetWatchlist('ID-TIBenignProperty')
    | where Notes has_any ("[SourceIPAddress]")
    | project IndicatorId, BenignProperty
;
let _TIExcludedSources = toscalar(
    _GetWatchlist('Activity-ExpectedSignificantActivity')
    | where Activity == "ThreatIndicatorSource"
    | summarize make_list(Auxiliar)
    );
let _TITableMatch = (ti_start: datetime, only_new_ti: boolean, table_start: datetime, table_end: datetime) {
    // Scheduled Analytics rules have a query period limit of 14d
    let ti_feed_start = max_of(ago(query_period), ti_start - iff(only_new_ti, ti_feed_sync_frequency + ti_feed_sync_duration, 0s));
    let ti_feed_end = now();
    let _Indicators =// materialize(
        union
            // Manually added indicators
            (ThreatIntelligenceIndicator
            | where TimeGenerated > ago(query_period) and ExternalIndicatorId has "indicator--"
            ),
            // Feed ingested indicators
            (ThreatIntelligenceIndicator
            | where TimeGenerated between (ti_feed_start .. ti_feed_end) and not(ExternalIndicatorId has "indicator--")
            )
        // Take the earliest TimeGenerated and the latest column info
        | summarize hint.strategy=shuffle
            minTimeGenerated = min(TimeGenerated),
            arg_max(TimeGenerated, Active, Description, ActivityGroupNames, IndicatorId, ThreatType, DomainName, Url, ExpirationDateTime, ConfidenceScore, AdditionalInformation, ExternalIndicatorId, NetworkIP, NetworkSourceIP, NetworkDestinationIP, EmailSourceIpAddress)
            by IndicatorId
        // Remove inactive or expired indicators
        | where not(not(Active) or ExpirationDateTime < ti_feed_end)
        // Remove feed indicators not received in the specified period
        | where not(TimeGenerated < ti_start and not(ExternalIndicatorId has "indicator--"))
        // Pick indicators that contain the desired entity type
        | mv-expand IPAddress = pack_array(NetworkIP, NetworkSourceIP, NetworkDestinationIP, EmailSourceIpAddress) to typeof(string)
        | where isnotempty(IPAddress)
        | extend TI_IPAddress = IPAddress
        // Remove indicators from specific sources
        | where not(AdditionalInformation has_any (_TIExcludedSources))
        // Remove excluded indicators with benign properties
        | join kind=leftanti _TIBenignProperty on IndicatorId, $left.IPAddress == $right.BenignProperty
        // Deduplicate indicators by IPAddress column, equivalent to using join kind=innerunique afterwards
        | summarize hint.strategy=shuffle
            minTimeGenerated = min(minTimeGenerated),
            take_any(*)
            by IPAddress
        // If we want only new indicators, remove indicators received previously
        | where not(only_new_ti and minTimeGenerated < ti_start)
    //)
    ;
    //let _IndicatorsLength = toscalar(_Indicators | summarize count());
    //let _IndicatorsPrefilter = toscalar(
    //    _Indicators
    //    | extend AuxiliarField = tostring(extract(@"([0-9A-Za-f]+)[\.\:]", 1, IPAddress))
    //    | summarize make_set_if(AuxiliarField, isnotempty(AuxiliarField), 10000)
    //);
    //let _IndicatorsPrefilterLength = array_length(_IndicatorsPrefilter);
    let _TableEvents =
        AuditLogs
        | where TimeGenerated between (table_start .. table_end)
        | extend UserPrincipalName = tostring(InitiatedBy.user.userPrincipalName)
        | extend IPAddress = tostring(InitiatedBy[tostring(bag_keys(InitiatedBy)[0])].ipAddress)
        // Filter events that may contain indicators
        | where not(Category == "UserManagement" and LoggedByService == "Self-service Password Management" and OperationName == "Self-service password reset flow activity progress" and Result == "failure")
        | where isnotempty(IPAddress)
        //| where not(_IndicatorsPrefilterLength < 10000 and not(InitiatedBy has_any (_IndicatorsPrefilter))) // "has_any" limit 10000
        //| where not(_IndicatorsLength < 1000000 and not(IPAddress in (toscalar(_Indicators | summarize make_list(TI_IPAddress))))) // "in" limit 1.000.000
        | project-rename AuditLogs_TimeGenerated = TimeGenerated
    ;
    _Indicators
    | join kind=inner hint.strategy=shuffle _TableEvents on IPAddress
    // Take only a single event by key columns
    //| summarize hint.strategy=shuffle take_any(*) by IPAddress, InitiatedBy = tostring(InitiatedBy)
    | project
        AuditLogs_TimeGenerated,
        Description, ActivityGroupNames, IndicatorId, ThreatType, DomainName, Url, ExpirationDateTime, ConfidenceScore, AdditionalInformation, TI_IPAddress, NetworkIP, NetworkSourceIP, NetworkDestinationIP, EmailSourceIpAddress,
        Category, OperationName, Result, ResultDescription, Identity, UserPrincipalName, IPAddress, InitiatedBy = tostring(InitiatedBy), LoggedByService, AdditionalDetails, TargetResources, CorrelationId
};
union// isfuzzy=true
    // Match      all indicators available                                    with current table events
    _TITableMatch(ago(ti_feed_sync_frequency + ti_feed_sync_duration), false, ago(query_frequency), now()),
    // Match      new indicators since last query execution                   with past table events
    _TITableMatch(ago(query_frequency), true, ago(table_query_lookback), ago(query_frequency))
| summarize arg_max(AuditLogs_TimeGenerated, *) by IndicatorId, InitiatedBy
| extend
    timestamp = AuditLogs_TimeGenerated,
    AccountCustomEntity = UserPrincipalName,
    IPCustomEntity = TI_IPAddress
