let account_threshold = 5;
AADNonInteractiveUserSignInLogs
//| where ResultType == "81016"
| where ResultType startswith "81"
| summarize DistinctAccountCount = dcount(UserId), DistinctAddresses = make_set(IPAddress) by ResultType
| where DistinctAccountCount > account_threshold
| mv-expand IPAddress = DistinctAddresses to typeof(string)
| join kind=rightsemi (union SigninLogs, AADNonInteractiveUserSignInLogs, ADFSSignInLogs) on IPAddress
| summarize
    StartTime = min(TimeGenerated),
    EndTime = max(TimeGenerated),
    UserPrincipalName = make_set(UserPrincipalName),
    UserAgent = make_set(UserAgent),
    take_any(ResultDescription, ResultSignature)
    by Type, IPAddress, ResultType
| project
    Type,
    StartTime,
    EndTime,
    IPAddress,
    ResultType,
    ResultDescription,
    ResultSignature,
    UserPrincipalName,
    UserAgent = iff(array_length(UserAgent) == 1, UserAgent[0], UserAgent)
