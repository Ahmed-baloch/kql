// This rule detects several accounts with authentication failures in AD FS from the same IP address, that presumably could be the result of password spray activity.
let query_frequency = 15m;
let query_period = 30m;
let account_threshold = 10;
let alert_name = "Password spray attack against AD FS";
let compromised_alert_name_addition = " - Compromised user";
//let _SuccessResultTypes = dynamic(["0", "50144"]);
let _SuccessResultTypes = toscalar(
    _GetWatchlist('ResultType-SignInLogsErrorCodes')
    | where Notes has_any ("[Expired]", "[Success]") and isnotempty(ResultDescription)
    | summarize make_list(ResultType)
);
ADFSSignInLogs
| where ingestion_time() between (ago(query_period)..now())
| where not(todynamic(AuthenticationDetails)[0].authenticationMethod == "Integrated Windows Authentication")
| summarize
    maxIngestionTime = max(ingestion_time()),
    FailureAccountCount = dcountif(UserPrincipalName, ResultType !in (_SuccessResultTypes)),
    SuccessAccountCount = dcountif(UserPrincipalName, ResultType in (_SuccessResultTypes)),
    FailureAccounts = make_set_if(UserPrincipalName, ResultType !in (_SuccessResultTypes), 250),
    SuccessAccounts = make_set_if(UserPrincipalName, ResultType in (_SuccessResultTypes)),
    arg_min(TimeGenerated, Category, OperationName, Location, AuthenticationRequirement, ConditionalAccessStatus, IsInteractive, NetworkLocationDetails, DeviceDetail, UserAgent, TokenIssuerType, TokenIssuerName, ResourceIdentity)
    by IPAddress
| where maxIngestionTime > ago(query_frequency) and FailureAccountCount > SuccessAccountCount and FailureAccountCount >= account_threshold
| extend
    AlertName = strcat(alert_name, case(
        array_length(SuccessAccounts) > 0, compromised_alert_name_addition,
        ""
    )),
    AlertSeverity = case(
        array_length(SuccessAccounts) > 0, "High",
        "Low"
    )
| mv-expand SuccessAccount = iff(array_length(SuccessAccounts) == 0, dynamic([""]), SuccessAccounts) to typeof(string)
| project
    TimeGenerated,
    Category,
    OperationName,
    IPAddress,
    Location,
    FailureAccountCount,
    SuccessAccountCount,
    SuccessAccount,
    SuccessAccounts,
    FailureAccounts,
    AuthenticationRequirement,
    ConditionalAccessStatus,
    IsInteractive,
    NetworkLocationDetails,
    DeviceDetail,
    UserAgent,
    TokenIssuerType,
    TokenIssuerName,
    ResourceIdentity,
    AlertName,
    AlertSeverity
